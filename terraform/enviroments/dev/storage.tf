resource "random_id" "bucket_suffix" {
  byte_length = 4
}

# CodeDeploy Artifacts Bucket
resource "aws_s3_bucket" "codedeploy_bucket" {
  bucket        = "converter-artifacts-${random_id.bucket_suffix.hex}"
  force_destroy = true
}

# Frontend (React) Bucket
resource "aws_s3_bucket" "frontend_bucket" {
  bucket        = "converter-frontend-${random_id.bucket_suffix.hex}"
  force_destroy = true
}

# Static Website Hosting configuration
resource "aws_s3_bucket_website_configuration" "frontend_hosting" {
  bucket = aws_s3_bucket.frontend_bucket.id

  index_document {
    suffix = "index.html"
  }

  error_document {
    key = "index.html"
  }
}

# Remove public access lock
resource "aws_s3_bucket_public_access_block" "frontend_public_access" {
  bucket = aws_s3_bucket.frontend_bucket.id

  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

# Bucket Public Read policy
resource "aws_s3_bucket_policy" "frontend_policy" {
  bucket = aws_s3_bucket.frontend_bucket.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid       = "PublicReadGetObject"
        Effect    = "Allow"
        Principal = "*"
        Action    = "s3:GetObject"
        Resource  = "${aws_s3_bucket.frontend_bucket.arn}/*"
      }
    ]
  })

  depends_on = [aws_s3_bucket_public_access_block.frontend_public_access]
}